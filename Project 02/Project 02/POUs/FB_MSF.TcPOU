<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_MSF" Id="{bd432739-5908-4875-a0fd-841e5bbb06d4}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_MSF
VAR_INPUT
	start: BOOL;
	stop : BOOL := TRUE;
	emg  : BOOL := TRUE;
	ack  : BOOL;
	fc1  : BOOL;
	fc2  : BOOL;
END_VAR
VAR_OUTPUT
	H1   : BOOL;
	HEMG : BOOL;
	M    : BOOL;
	// il nastro è libero
	libero : BOOL;
END_VAR
VAR
	
	// abilitazione dell’automatismo
	enable : BOOL := TRUE;
	// condizioni di transizione
	tr_S0_S1 : BOOL;
	tr_S1_S2 : BOOL;
	tr_S2_S3 : BOOL;
	tr_S3_S1 : BOOL;
	tr_S3_S0 : BOOL;
	tr_SE_SA : BOOL;
	tr_SA_S0 : BOOL;
	// variabile di stato
	stato : STATI := STATI.S0;
	// timers
	T31 : TON();
	TA0 : TON();
	// triggers per segnalazione nastro libero
	pckin : R_TRIG();
	pckout: R_TRIG();
	// lampeggio
	osc : FB_oscillatore();	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*
1.aggiornamento delle condizioni di abilitazione (stop, allarmi, etc)
2.aggiornamento di timers, contatori, triggers sui fronti
3.valutazione della condizione di arresto nominale
4.valutazione della condizione di arresto di emergenza
5.aggiornamento delle condizioni di transizione di stato
6.switch-case della MSF
7.comando delle uscite
*)
// 1. aggiornamento condizioni di abilitazione
enable := stop AND emg;
// 2. aggiornamento timers, contatori, triggers sui fronti
T31(IN := (stato = STATI.S3 AND enable), PT := T#10S);
TA0(IN := (stato = STATI.SA AND enable), PT := T#10S);
pckin(clk:= fc1);
pckout(clk:= fc2);
osc(enable := (stato = STATI.SA), t_high := T#500MS, t_low := T#500MS);

// 3. condizione di arresto nominale
IF NOT stop AND emg THEN
	stato := STATI.S0;	
END_IF
// 4. condizione di arresto di emergenza
IF NOT emg THEN
	stato := STATI.SE;
END_IF
// 5. aggiornamento delle condizioni di transizione
tr_S0_S1 := enable AND start;
tr_S1_S2 := enable AND fc1 AND NOT fc2;
tr_S2_S3 := enable AND fc2;
tr_S3_S1 := enable AND fc1 AND NOT fc2 AND NOT T31.Q;
tr_S3_S0 := enable AND T31.Q;
tr_SE_SA := enable AND ack;
tr_SA_S0 := enable AND TA0.Q;
// 6. switch-case della MSF
CASE stato OF
STATI.S0:
	IF tr_S0_S1 THEN
		stato := STATI.S1;
	END_IF
STATI.S1:
	IF tr_S1_S2 THEN
		stato := STATI.S2;
	END_IF
STATI.S2:
	IF tr_S2_S3 THEN
		stato := STATI.S3;
	END_IF
STATI.S3:
	IF tr_S3_S1 THEN
		stato := STATI.S1;
	ELSIF tr_S3_S0 THEN
		stato := STATI.S0;
	END_IF
STATI.SE:
	IF tr_SE_SA THEN
		stato := STATI.SA;
	END_IF
STATI.SA:
	IF tr_SA_S0 THEN
		stato := STATI.S0;
	END_IF
ELSE
	stato := STATI.SE;
END_CASE
// 7. comando uscite
H1 := stato <> STATI.S0;
HEMG := (stato = STATI.SE) OR osc.q;
M := (stato = STATI.S2) OR (stato = STATI.S3);
// nastro libero se (enable = true e  pckout.q) o (fc1 libero in S1);
// nastro occupato se enable = false o fc1.rise o stato = S0
IF (enable AND (pckout.q)) OR (stato = STATI.S1 AND NOT fc1) THEN
	libero := TRUE;
ELSIF (NOT enable) OR pckin.Q OR stato = STATI.S0 THEN
	libero := FALSE;
END_IF




]]></ST>
    </Implementation>
    <LineIds Name="FB_MSF">
      <LineId Id="205" Count="0" />
      <LineId Id="207" Count="8" />
      <LineId Id="9" Count="0" />
      <LineId Id="56" Count="0" />
      <LineId Id="40" Count="0" />
      <LineId Id="42" Count="2" />
      <LineId Id="47" Count="0" />
      <LineId Id="216" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="48" Count="2" />
      <LineId Id="58" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="60" Count="1" />
      <LineId Id="63" Count="0" />
      <LineId Id="62" Count="0" />
      <LineId Id="65" Count="2" />
      <LineId Id="70" Count="1" />
      <LineId Id="73" Count="0" />
      <LineId Id="81" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="94" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="96" Count="0" />
      <LineId Id="100" Count="2" />
      <LineId Id="99" Count="0" />
      <LineId Id="104" Count="2" />
      <LineId Id="103" Count="0" />
      <LineId Id="108" Count="2" />
      <LineId Id="120" Count="1" />
      <LineId Id="107" Count="0" />
      <LineId Id="112" Count="2" />
      <LineId Id="111" Count="0" />
      <LineId Id="116" Count="2" />
      <LineId Id="115" Count="0" />
      <LineId Id="122" Count="1" />
      <LineId Id="91" Count="0" />
      <LineId Id="131" Count="0" />
      <LineId Id="130" Count="0" />
      <LineId Id="137" Count="0" />
      <LineId Id="142" Count="0" />
      <LineId Id="150" Count="0" />
      <LineId Id="217" Count="0" />
      <LineId Id="149" Count="0" />
      <LineId Id="152" Count="0" />
      <LineId Id="156" Count="1" />
      <LineId Id="153" Count="0" />
      <LineId Id="173" Count="0" />
      <LineId Id="139" Count="0" />
      <LineId Id="68" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="41" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>